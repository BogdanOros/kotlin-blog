group 'com.boros'
version '1.0-SNAPSHOT'

buildscript {

    ext.kotlin_version = '1.2.10'
    ext.spring_boot_version = '1.5.9.RELEASE'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }

}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

jar {
    baseName = 'blog'
    version = '1.0.0'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.springframework.boot:spring-boot-starter-web:$spring_boot_version"

    testCompile "org.springframework.boot:spring-boot-starter-test:$spring_boot_version"
}

sourceSets {
    integration_test {
        java {
            srcDir file('src/integration_test/kotlin')
        }
        resources {
            srcDir file('src/integration_test/resources')
        }
    }
}

task integration_test(type: Test) {
    group 'verification'

    testClassesDir = sourceSets.integration_test.output.classesDir
    classpath = sourceSets.integration_test.runtimeClasspath
}

check.dependsOn integration_test

dependencies {
    integration_testCompile sourceSets.main.output
    integration_testCompile configurations.testCompile
    integration_testCompile sourceSets.test.output
    integration_testRuntime configurations.testRuntime
}

idea {
    module {
        testSourceDirs += file('src/integration_test/kotlin')
    }
}